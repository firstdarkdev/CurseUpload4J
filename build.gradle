plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.13.0"
}

group 'me.hypherionmc.modutils'
version '1.0.5'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    shadeMe
    implementation.extendsFrom shadeMe
}

repositories {
    mavenCentral()
}

dependencies {
    shadeMe 'commons-io:commons-io:2.11.0'
    shadeMe group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    shadeMe group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    shadeMe group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.14'
    shadeMe group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'

    // Logging
    shadeMe 'org.apache.logging.log4j:log4j-api:2.20.0'
    shadeMe 'org.apache.logging.log4j:log4j-core:2.20.0'
    shadeMe 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.getByName("shadeMe")]
    archiveClassifier.set(null)
}

jar.finalizedBy(shadowJar)

spotless {
    java {
        targetExclude("src/test/**")
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }

    repositories {
        maven {
            url System.getenv('MAVEN_URL')
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}
