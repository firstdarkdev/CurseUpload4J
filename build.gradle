plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.13.0"
}

group maven_group
version "${version_major}.${version_minor}.${version_patch}"
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    shadeMe
    implementation.extendsFrom shadeMe
}

repositories {
    mavenCentral()
}

dependencies {
    shadeMe group: 'commons-io', name: 'commons-io', version: apache_commons
    shadeMe group: 'net.sf.trove4j', name: 'trove4j', version: trove4j
    shadeMe group: 'com.google.code.gson', name: 'gson', version: gson
    shadeMe group: 'org.apache.httpcomponents', name: 'httpmime', version: httpmime
    shadeMe group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: httpclient
    shadeMe group: 'org.jetbrains', name: 'annotations', version: jetbrains

    // Logging
    shadeMe group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j
    shadeMe group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j
    shadeMe group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.shadeMe]
    archiveClassifier.set(null)
}

jar.finalizedBy(shadowJar)

spotless {
    java {
        targetExclude("src/test/**")
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }

    repositories {
        maven {
            url System.getenv('MAVEN_URL')
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}
